pipeline {
    agent any

    parameters {
        string(name: 'API_KEY', defaultValue: '', description: 'API key to access the API')
        string(name: 'name', defaultValue: '', description: 'Name of the client container')
        string(name: 'containerId', defaultValue: '', description: 'ID of the WebApp container')
        choice(name: 'ACTION', choices: ['start', 'stop', 'remove'], description: 'Action to perform on the container stack')
    }

    environment {
        WEB_API_URL = "http://192.168.56.1:3000/api/docker/updated"
    }

    stages {
        stage('Validate API Key') {
            steps {
                script {
                    if (!params.API_KEY) {
                        error("API key is required to start the pipeline.")
                    }
                }
            }
        }

        stage('Validate Container ID') {
            steps {
                script {
                    if (!params.containerId?.trim()) {
                        error("Container ID is required to find the stack.")
                    }
                }
            }
        }

        stage('Find Stack Containers') {
            steps {
                script {
                    echo "Finding all containers belonging to WebApp container ID: ${params.containerId}"

                    def shortContainerId = params.containerId.length() > 12 ? params.containerId.substring(0,12) : params.containerId

                    def stackContainersOutput = sh(
                        script: """
                        docker ps -a --format '{{.ID}} {{.Names}}' | grep '${shortContainerId}' | awk '{print \$1}'
                        """,
                        returnStdout: true
                    ).trim()

                    if (!stackContainersOutput) {
                        error("No containers found for WebApp container ID: ${params.containerId}")
                    }

                    def stackContainers = stackContainersOutput.split("\n")

                    echo "Stack containers found: ${stackContainers.join(', ')}"
                    env.STACK_CONTAINERS = stackContainers.join(' ')
                }
            }
        }

        stage('Manage Container Stack') {
            steps {
                script {
                    def actionCommand = ""
                    
                    if (params.ACTION == 'stop') {
                        echo "Stopping stack: ${env.STACK_CONTAINERS}"
                        actionCommand = "docker stop ${env.STACK_CONTAINERS}"
                    } else if (params.ACTION == 'remove') {
                        echo "Removing stack: ${env.STACK_CONTAINERS}"
                        actionCommand = "docker rm -f ${env.STACK_CONTAINERS}"
                    } else if (params.ACTION == 'start') {
                        echo "Starting stack: ${env.STACK_CONTAINERS}"
                        actionCommand = "docker start ${env.STACK_CONTAINERS}"
                    } else {
                        error("Invalid action specified: ${params.ACTION}")
                    }

                    sh actionCommand
                }
            }
        }
    }

    post {
        always {
            script {
                def stackStatus = sh(
                    script: "docker ps --filter 'id=${params.containerId}' --format '{{.ID}}'",
                    returnStdout: true
                ).trim() ? "running" : "stopped"

                def jsonData = "{ \"containerId\": \"${params.containerId}\", \"status\": \"${stackStatus}\", \"containerName\": \"${params.name}\" }"

                sh "curl -X POST -H 'Content-Type: application/json' -H 'x-api-key: ${params.API_KEY}' -d '${jsonData}' ${WEB_API_URL}"
                echo "Stack ${params.name} is now ${stackStatus}"
            }
        }
    }
}
